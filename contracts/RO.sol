pragma solidity ^0.8.17;
import "hardhat/console.sol";

library RO {

    // Optimization to flip endianness of uint256 in terms of gas cost
    // https://github.com/summa-tx/bitcoin-spv/pull/107#issuecomment-578010422
    function reverse(uint256 input) internal pure returns (uint256 v) {
        v = input;

        // swap bytes
        v = ((v & 0xFF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00) >> 8) |
            ((v & 0x00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF) << 8);

        // swap 2-byte long pairs
        v = ((v & 0xFFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000) >> 16) |
            ((v & 0x0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF) << 16);

        // swap 4-byte long pairs
        v = ((v & 0xFFFFFFFF00000000FFFFFFFF00000000FFFFFFFF00000000FFFFFFFF00000000) >> 32) |
            ((v & 0x00000000FFFFFFFF00000000FFFFFFFF00000000FFFFFFFF00000000FFFFFFFF) << 32);

        // swap 8-byte long pairs
        v = ((v & 0xFFFFFFFFFFFFFFFF0000000000000000FFFFFFFFFFFFFFFF0000000000000000) >> 64) |
            ((v & 0x0000000000000000FFFFFFFFFFFFFFFF0000000000000000FFFFFFFFFFFFFFFF) << 64);

        // swap 16-byte long pairs
        v = (v >> 128) | (v << 128);
    }

    function spongeAbsorbSqueeze(uint256[] memory transcript) internal pure returns (uint256) {
        uint256[] memory transcript_flip_endian = new uint256[](transcript.length);
        for(uint i = 0; i < transcript.length; i++){
            transcript_flip_endian[i] = reverse(transcript[i]);
        }
        uint256 hash = uint256(sha256(abi.encodePacked(transcript_flip_endian)));
        // Truncate the hash to the lower 128 bits
        uint256 mask = 0x00000000000000000000000000000000FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF;
        return reverse(hash) & mask;
    }
}